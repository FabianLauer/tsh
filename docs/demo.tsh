class RequireConfigPaths {
	const vs = "monaco-editor/vs"
}

class RequireConfig {
	const paths = RequireConfigPaths()
}

/// The main function of the demo.
func main() -> Void {
	require.config(RequireConfig())
	require(window.createJSArray('vs/editor/editor.main'), func () {
		startDemo(monaco)
	})
}


func startDemo(monaco: Any) -> Void {
	const sourceEditor = createEditor(monaco, "source-container", "swift", false, getSampleCode())
	const compiledEditor = createEditor(monaco, "compiled-container", "javascript", true, "")

	let lastRun = 0
	let lastRunTimeout

	const recompileAndRun = func() {
		const result = compile(sourceEditor, compiledEditor)

		clearTimeout(lastRunTimeout)

		if result == CompileResult.CompilationSuccessful {
			const now = Date.now()

			if now - lastRun < 100 {
				lastRunTimeout = setTimeout(recompileAndRun)
			} else {
				run(compiledEditor.getValue())
			}	
		}
	}

	// compile once on start
	recompileAndRun()

	// bind events
	sourceEditor.onDidType(recompileAndRun)
	sourceEditor.onDidPaste(recompileAndRun)
	sourceEditor.onDidBlurEditor(recompileAndRun)
	sourceEditor.onDidChangeModelContent(recompileAndRun)
}


class MonacoEditorOptions {
	let value: String
	let language: String
	let readOnly: Bool

	const lineNumbers = true
	const roundedSelection = false
	const scrollBeyondLastLine = true
	const theme = "vs-light"
	const fontSize = 11
	const wordWrap = true
	const folding = true
}


func createEditor(monaco: Any, containerId: String, language: String, readOnly: Bool, content: String) -> Any {
	const options = MonacoEditorOptions()

	options.value = content
	options.language = language
	options.readOnly = readOnly

	return monaco.editor.create(document.getElementById(containerId), options)
}


enum CompileTarget {
	JavaScript,
	TypeScriptDeclaration
}


enum CompileResult {
	CompilationSuccessful,
	CompilationFailed
}


func compile(sourceEditor: Any, compiledEditor: Any) -> CompileResult {
	clearProblems()

	let sourceCode = sourceEditor.getValue()
	let code = ""
	let compiled = false
	let compiledCode
	let result: CompileResult = CompileResult.CompilationSuccessful

	// compile to JavaScript
	try {
		compiledCode = compileToTarget(sourceCode, CompileTarget.JavaScript)
		compiled = true
	} catch as err {
		result = CompileResult.CompilationFailed
		renderProblem(ProblemType.CompileTime, err)
	}
	if compiled {
		code += compiledCode
	}

	// compile to TypeScript declarations
	try {
		compiledCode = compileToTarget(sourceCode, CompileTarget.TypeScriptDeclaration)
		compiled = true
	} catch {
		compiled = false
		result = CompileResult.CompilationFailed
	}
	if compiled {
		compiledCode = compiledCode.replace(_new(RegExp, "\t+", "g"), "  ")

		code += "\n/* --- TypeScript Declarations --- */\n"
		code += "/*\n\n" + compiledCode + "\n*/"
	}

	compiledEditor.setValue(tryBeautifyJavaScript(code))
	return result
}

func run(code: String) -> Void {
	clearOutput()

	js = "(function(exports, print, require) {"
	js += code

	// auto-call the main function
	js += "; main();"

	// inject dependencies
	js += "})({}, window.renderOutput, function() { throw new Error('import/reuqire not supported.') })"

	try {
		window.runJavaScript(js)
	} catch as runtimeErr {
		renderProblem(ProblemType.RunTime, runtimeErr)
	}
}


func compileToTarget(sourceCode: String, compileTarget: CompileTarget) -> Void {
	const api = compiler.CompilerApi.create()
	let target: Int

	if compileTarget == CompileTarget.JavaScript {
		target = 0
	} else {
		target = 1
	}

	return api.compileSourceCode(sourceCode, target)
}


func tryBeautifyJavaScript(jsCode) -> String {
	try {
		return window.js_beautify(jsCode)
	} catch {
		return jsCode
	}
}


enum ProblemType {
	CompileTime,
	RunTime
}

func getProblemsPanelContentElement() -> HTMLElement {
	return document.getElementById("problems-panel-content")
}


func clearProblems() -> Void {
	const problemsPanelContent = getProblemsPanelContentElement()
	problemsPanelContent.innerHTML = ""
	const content = document.createElement("span")
	content.innerText = "No problems."
	problemsPanelContent.appendChild(content)
}


func renderProblem(type: ProblemType, error: Any) -> Void {
	const problemsPanelContent = getProblemsPanelContentElement()
	problemsPanelContent.innerHTML = ""
	let problem = document.createElement("pre")
	
	if type == ProblemType.CompileTime {
		problem.innerText = "Compiler Error:"
	} else {
		problem.innerText = "Runtime Error:"
	}

	problem.innerText += "\n" + error.message
	problemsPanelContent.appendChild(problem)

	console.warn("Problem:", type, error)
}


func getOutputPanelContentElement() -> HTMLElement {
	return document.getElementById("output-panel-content")
}


func clearOutput() -> Void {
	const outputPanelContent = getOutputPanelContentElement()
	outputPanelContent.innerHTML = ""
}


func renderOutput(output: String) -> Void {
	const outputPanelContent = getOutputPanelContentElement()
	outputPanelContent.innerHTML += output

	console.log("Output:", output)
}
